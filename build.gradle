plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"
}

apply from: "$rootDir/gradle/versioning.gradle"
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'ch.so.agi.ilivalidator'

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://jars.interlis.ch" }
}

dependencies {
    api 'ch.interlis:iox-ili:1.20.4'
    //api 'ch.interlis:iox-ili:1.20.12-SNAPSHOT'
    api 'ch.interlis:ili2c-tool:4.7.7'
    api 'org.jgrapht:jgrapht-core:1.3.0'
    api 'org.jgrapht:jgrapht-ext:1.3.0'
    
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def githubRepoUrl = "https://github.com/sogis/ilivalidator-extension-functions"
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://github.com/sogis/ilivalidator-extension-functions/blob/master/LICENSE"
        }
    }
    developers {
        developer {
            id "edigonzales"
            name "Stefan Ziegler"
            email "stefan.ziegler@bd.so.ch"
        }
    }

    scm {
        url githubRepoUrl
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId 'ilivalidator-extension-functions'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Extension functions for ilivalidator')
                root.appendNode('name', 'ilivalidator-extension-functions')
                root.appendNode('url', githubRepoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.env.bintrayUser
    key = System.env.bintrayKey
    publications = ['mavenPublication']
    publish = true
    
    pkg {
        repo = 'ilivalidator-extension-functions'
        name = 'ilivalidator-extension-functions'
        userOrg = 'sogis'
        licenses = ['MIT']
        labels = ['interlis', 'ilivalidator', 'validation']        
        vcsUrl = githubRepoUrl
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}

